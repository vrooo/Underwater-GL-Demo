#version 430 core

layout (local_size_x = 32, local_size_y = 32, local_size_z = 1) in;

uniform sampler2D freqWaveTex;
layout (rg32f) uniform writeonly image2D curFreqWaveTex;
uniform float t;
uniform int fourierGridSize;

void main()
{
	ivec2 texCoord = ivec2(gl_GlobalInvocationID.xy);
	vec4 freqWaveInfo = texelFetch(freqWaveTex, texCoord, 0); // h0.x, h0.y, omega
	vec2 h0 = freqWaveInfo.rg, h0neg = texelFetch(freqWaveTex, fourierGridSize - texCoord, 0).rg;
	float omega = freqWaveInfo.b;

	float phase = omega * t, sinp = sin(phase), cosp = cos(phase);
	vec2 ht = vec2(cosp * (h0.x + h0neg.x) - sinp * (h0.y + h0neg.y), cosp * (h0.y - h0neg.y) + sinp * (h0.x - h0neg.x));
	imageStore(curFreqWaveTex, texCoord, vec4(ht, 0.0f, 1.0f));
}